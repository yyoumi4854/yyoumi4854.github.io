<%@ CodePage="65001" Language="vbscript"%>
<!--#include virtual="/myoffice/common/common.inc"-->
<!-- #include virtual ="/myoffice/common/adovbs.inc" -->
<!--#include virtual="/myoffice/common/jsonObject.class.asp"-->
<%
Session.CodePage = "65001" : Response.CharSet = "utf-8"

'On Error Resume Next

post_str = ""

For Each Key In Request.Form
	post_str = post_str & CStr(Key) & ":" & Request.Form(Key) & "|"
Next

sql =		"insert into LOG_FORM_CHECK (				"
sql = sql & "	idx, page, form_value					"
sql = sql & ") values (									"
sql = sql & ""	&	"LOG_SEQ.NextVal"			& ",	"
sql = sql & "'"	&	"wallet_request_proc.html"	& "',	"
sql = sql & "'"	&	post_str					& "'	"
sql = sql & ")											"
DBConn.Execute(sql)

work_user	= Trim(Request.Form("work_user"))
cyber_kind	= Trim(Request.Form("cyber_kind"))
userid		= Trim(Request.Form("userid"))
req_amt		= Trim(Request.Form("req_amt"))		: req_amt	= Replace(req_amt,",","")
fee_amt		= Trim(Request.Form("fee_amt"))		: fee_amt	= Replace(fee_amt,",","")
trans_amt	= Trim(Request.Form("trans_amt"))	: trans_amt = Replace(trans_amt,",","")

point_method= Trim(Request.Form("point_method"))

request_method	= UCase(Trim(Request.Form("request_method")))

bank_name	= Trim(Request.Form("bank_name"))
bank_acc	= Trim(Request.Form("bank_acc"))
bank_owner	= Trim(Request.Form("bank_owner"))
bank_amt	= Trim(Request.Form("bank_amt"))	: bank_amt = Replace(bank_amt,",","")

coin_address= Trim(Request.Form("coin_address"))
coin_price	= Trim(Request.Form("coin_price"))
coin_amt	= Trim(Request.Form("coin_amt"))	: coin_amt = Replace(coin_amt,",","")

If coin_address <> "" Then
	coin_price	= Replace(coin_price,",","")
	coin_amt	= Replace(coin_amt,",","")
Else
	coin_price	= 0
	coin_amt	= 0
End If

If request_method = "BANK" Then
	coin_address= ""
	coin_price	= 0
	coin_amt	= 0
ElseIf request_method = "COIN" Then
	bank_name	= ""
	bank_acc	= ""
	bank_owner	= ""
	bank_amt	= 0
End If

If point_method = "GTXp" Then
	use_GTXp = CCur(req_amt)
Else
	use_GTXp = 0
End If

inj_chk		= check_injection("")

If inj_chk <> "OK" Then
	rtn_msg		= "Some characters are not allowed.\nPlease check your input data."
	rtn_state	= "FAIL"
	rtn_url		= ""
Else

	sql = "select	sum(amt) as tot_amt from cyber_wallet where userid = '" & Session("userid") & "'"
	Set rs = DBConn.Execute(sql)
	If Not rs.EOF Then
		tot_amt = CCur(rs("tot_amt"))
	Else
		tot_amt = 0
	End If
	rs.Close	: Set rs = Nothing

	If tot_amt < CCur(req_amt) Then
		SP_RESULT = "FAIL"
		SP_ERROR = "The balance is insufficient."	'"잔액이 부족합니다."
	Else
		set objCom  = Server.CreateObject("ADODB.Command")
		With objCom

			.ActiveConnection = DBConn
			.CommandType = 4
			.CommandText = "proc_wallet_use"

			.Parameters.Append .CreateParameter("SP_CYBER_KIND",	adVarChar,	adParamInput,	30,	cyber_kind		)
			.Parameters.Append .CreateParameter("SP_USERID",		adVarChar,	adParamInput,	20,	work_user		)
			.Parameters.Append .CreateParameter("SP_TARGET_ID",		adVarChar,	adParamInput,	20,	rcv_userid		)
			.Parameters.Append .CreateParameter("SP_PRODUCT",		adVarChar,	adParamInput,  255,	product			)
			.Parameters.Append .CreateParameter("SP_QTY",			adVarChar,	adParamInput,  255,	qty				)
			.Parameters.Append .CreateParameter("SP_REQ_AMT",		adNumeric,	adParamInput,	 0,	req_amt			)
			.Parameters.Append .CreateParameter("SP_FEE_AMT",		adNumeric,	adParamInput,	 0,	fee_amt			)
			.Parameters.Append .CreateParameter("SP_TOT_AMT",		adNumeric,	adParamInput,	 0,	trans_amt		)

			.Parameters.Append .CreateParameter("SP_USE_GTXP",		adNumeric,	adParamInput,	 0,	use_GTXp		)

			.Parameters.Append .CreateParameter("SP_REQUEST_KIND",	adVarChar,	adParamInput,   10,	request_method	)

			.Parameters.Append .CreateParameter("SP_BANK_CD",		adVarChar,	adParamInput,   50,	""				)
			.Parameters.Append .CreateParameter("SP_BANK_NAME",		adVarChar,	adParamInput,  150, bank_name		)
			.Parameters.Append .CreateParameter("SP_BANK_ACC",		adVarChar,	adParamInput,	50,	bank_acc		)
			.Parameters.Append .CreateParameter("SP_BANK_OWNER",	adVarChar,	adParamInput,	30,	bank_owner		)
			.Parameters.Append .CreateParameter("SP_BANK_AMT",		adNumeric,	adParamInput,	 0,	bank_amt		)

			.Parameters.Append .CreateParameter("SP_COIN_ADDRESS",	adVarChar,	adParamInput,  255,	coin_address	)
			.Parameters.Append .CreateParameter("SP_COIN_PRICE",	adNumeric,	adParamInput,    0, coin_price		)
			.Parameters.Append .CreateParameter("SP_COIN_AMT",		adNumeric,	adParamInput,    0, coin_amt		)

			.Parameters.Append .CreateParameter("SP_WORK_USER",		adVarChar,	adParamInput,	20,	work_user		)
			.Parameters.Append .CreateParameter("SP_REMARKS",		adVarChar,	adParamInput,  255,	""				)
			.Parameters.Append .CreateParameter("SP_RESULT",		adVarChar,	adParamOutput,	50					)
			.Parameters.Append .CreateParameter("SP_ERROR",			adVarChar,	adParamOutput,1000					)
			.Execute  , , adExecuteNoRecords
		End With
			SP_RESULT	= objCom.Parameters("SP_RESULT").VALUE
			SP_ERROR	= objCom.Parameters("SP_ERROR").VALUE
		Set objCom = Nothing
	End If

	If SP_RESULT = "SUCCESS" Then
		rtn_msg		= SP_ERROR
		rtn_state	= "SUCCESS"
		rtn_url		= "/myoffice/e_wallet/transaction_detail.html"
	Else
		rtn_msg		= SP_ERROR
		rtn_state	= "FAIL"
		rtn_url		= ""
	End If

End If

    Set JSONParse = New JSONobject

	JSONParse.Add "rtn_msg" , rtn_msg
	JSONParse.Add "rtn_state", rtn_state
	JSONParse.Add "rtn_url", rtn_url

	JSONParse.Write()
%>